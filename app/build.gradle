/*
 * Code Scanner. An android app to scan and create codes(barcodes, QR codes, etc)
 * Copyright (C) 2021 Lucius Chee Zihan
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published
 * by the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'

android {


    compileSdkVersion 33

    defaultConfig {
        applicationId "com.czlucius.scan"
        minSdkVersion 23
        targetSdkVersion 33
        versionCode 17
        versionName "1.9"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

        }
        debug {
        	applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }

    }

    // Add Product Flavors for Google Play, F-Droid, and general release versions.
    flavorDimensions "standard"
    productFlavors {
        play {
            dimension "standard"
        }
        general {
            dimension "standard"
            getIsDefault().set(true)
        }
        foss {
            dimension "standard"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment:2.5.3'
    implementation 'androidx.navigation:navigation-ui:2.5.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "androidx.annotation:annotation:1.3.0"
    implementation 'androidx.preference:preference:1.2.0'

    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    testImplementation 'androidx.test:runner:1.4.0'


    // Leak Canary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.10'




    implementation "androidx.coordinatorlayout:coordinatorlayout:1.2.0"
    implementation 'com.google.android.material:material:1.8.0'


    // CameraX:
    def camerax_version = "1.1.0-alpha12"
    // CameraX core library using camera2 implementation
    implementation "androidx.camera:camera-camera2:$camerax_version"
    // CameraX Lifecycle Library
    implementation "androidx.camera:camera-lifecycle:$camerax_version"
    // CameraX View class
    implementation "androidx.camera:camera-view:1.0.0-alpha32"


    // ML Kit on GMS:
    implementation 'com.google.mlkit:barcode-scanning:17.0.2'

    // Room:
    implementation "androidx.room:room-runtime:2.4.0"
    annotationProcessor "androidx.room:room-compiler:2.4.0"


    // Moshi serialization library:
    implementation("com.squareup.moshi:moshi:1.11.0")

    // ZXing:
    implementation 'com.google.zxing:core:3.3.2'

    // Color Picker:
    implementation 'com.azeesoft.lib.colorpicker:colorpicker:1.0.8@aar'

    // VCard creator:
    implementation 'com.googlecode.ez-vcard:ez-vcard:0.11.2'

    // App Onboarding:
    implementation 'com.heinrichreimersoftware:material-intro:2.0.0'
    // ACRA crash reporting:
    implementation "ch.acra:acra-mail:5.9.0-rc2"



    // region Google Play specific libraries
    playImplementation 'com.google.android.gms:play-services-ads:20.5.0'

    // For apps targeting Android 12, add WorkManager dependency.
    constraints {
        playImplementation('androidx.work:work-runtime:2.7.0') {
            because '''androidx.work:work-runtime:2.1.0 pulled from play-services-ads
                   has a bug using PendingIntent without FLAG_IMMUTABLE or
                   FLAG_MUTABLE and will fail in apps targeting S+.'''
        }
    }

    // endregion


}
repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}
